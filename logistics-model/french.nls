breed [french-corps a-french-corps]

french-corps-own [units current-junction speed attrition loss-threshold plunder]

globals [destruction]

to setup-french
  set-default-shape french-corps "circle"
  
  create-french-corps 2 [
    ;let start-junction one-of junctions with [label = "Le Cateau"]
    let start-junction one-of junctions with [label = "Cambrai"]
    setxy [xcor] of start-junction [ycor] of start-junction
    set current-junction start-junction
  ]
  
  ask french-corps [
    set color blue
    set size 1
    set speed allied-speed * map-scale * time-scale
    set units allied-num-units
    set loss-threshold allied-num-units - allied-loss-threshold
    set plunder track-damage
  ]
  
  set destruction 0
end

to step-french
  ask french-corps [
    if-else destruction >= destruction-capacity or
    count german-corps in-radius retreat-distance * map-scale > 0 or
    units < loss-threshold [
      french-retreat self
    ][
      french-plunder self
    ]
  ]
end

to french-retreat [my-french-corps]
  let retreat-radius min (list 2.0 retreat-distance)
  if count german-corps in-radius retreat-radius = 0 [
    ;set loss-threshold units - allied-loss-threshold
    set plunder track-damage
  ]
  move-french my-french-corps one-of junctions with [label = "Paris"]
end

to french-plunder [my-french-corps]
  nw:set-context junctions roads
  ask my-french-corps [
    if distance current-junction <= speed [
      move-to current-junction
      let current-junction-label [label] of current-junction
      let my-current-station one-of stations with [label = current-junction-label]
      
      if-else my-current-station != nobody [
        let min-link nobody
        
        if my-current-station != nobody [
          ask my-current-station [
            set min-link min-one-of my-out-tracks with [health > track-damage-threshold] [weight]
          ]
        ]
        
        if (min-link != nobody) [
          let damage-given plunder * time-scale
          set destruction destruction + damage-given
          ask min-link [set health health - damage-given]
        ]
      ][
        let paris-junction one-of junctions with [label = "Paris"]
        let dist-paris distance paris-junction
        let dest-junction item 0 sort-by [
          [junction1 junction2] -> distance junction1 < distance junction2
        ] junctions with [distance paris-junction <= dist-paris]
        move-french self dest-junction
      ]
    ]
  ]
end

to move-french [my-french-corps dest-junction]
  ask my-french-corps [
    if-else distance current-junction <= speed [
      move-to current-junction
      let shortest-path (list current-junction)
      ask current-junction [
        nw:set-context junctions roads
        set shortest-path nw:turtles-on-weighted-path-to dest-junction weight
      ]
      let next-junction item 0 shortest-path
      if length shortest-path > 1 [set next-junction item 1 shortest-path]
      face next-junction
      set current-junction next-junction
    ][
      set speed allied-speed * map-scale * time-scale
      forward speed * (random-float 0.5 + 0.75)
    ]
  ]
end
